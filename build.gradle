buildscript {
	repositories { jcenter() }
	dependencies { classpath 'org.minimallycorrect.gradle:DefaultsPlugin:0.0.54' }
}
plugins {
	id "java-gradle-plugin"
	id "maven"
	id "com.gradle.plugin-publish" version "0.9.2"
}
apply plugin: 'org.minimallycorrect.gradle.DefaultsPlugin'

group="org.minimallycorrect.gradle"

minimallyCorrectDefaults {
	description = "Sets up sensible defaults for our gradle projects to avoid boilerplate"
	labels = ["gradle", "gradle-plugin"]
	languageLevel = null
	lombokDependencyCoordinates.clear()
	configureProject(project)
}

tasks.withType(JavaCompile) {
	options.compilerArgs.addAll(['--release', '8'])
}

repositories {
	jcenter()
	maven { url = "https://repo.nallar.me/" }
	maven { url = "https://plugins.gradle.org/m2/" }
	maven { url = "https://files.minecraftforge.net/maven" }
	maven { url = "https://jitpack.io" }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 30, 'seconds' }

dependencies {
	api gradleApi()
	compileOnly 'com.google.guava:guava:25.1-jre'
	compileOnly("net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT") {
		transitive = false
	}
	compileOnly("gradle.plugin.com.matthewprenger:CurseGradle:1.4.0") {
		transitive = false
	}
	implementation "com.diffplug.spotless:spotless-plugin-gradle:3.26.1"
	implementation "org.shipkit:shipkit:2.2.7"
	annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.2.0'
}

gradlePlugin {
	plugins {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			implementationClass = 'org.minimallycorrect.gradle.DefaultsPlugin'
		}
	}
}

pluginBundle {
	website = 'http://www.minimallycorrect.org/'
	vcsUrl = 'https://github.com/MinimallyCorrect/DefaultsPlugin'
	description = 'Sets up sensible defaults for our gradle projects to avoid boilerplate '
	tags = ['gradle', 'gradle-plugin']
	plugins << {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			displayName = 'MinimallyCorrect Defaults Plugin'
		}
	}
}
