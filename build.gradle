buildscript {
	repositories { jcenter() }
	dependencies { classpath 'org.minimallycorrect.gradle:DefaultsPlugin:0.1.11' }
}
plugins {
	id "java-gradle-plugin"
	id "maven-publish"
	id "com.gradle.plugin-publish" version "0.12.0"
	id "com.jfrog.bintray" version "1.8.5"
	id "org.shipkit.shipkit-auto-version" version "1.1.0"
	id "org.shipkit.shipkit-changelog" version "0.2.21"
	id "org.shipkit.shipkit-github-release" version "0.2.21"
}
apply plugin: 'org.minimallycorrect.gradle.DefaultsPlugin'

group="org.minimallycorrect.gradle"

minimallyCorrectDefaults {
	description = "Sets up sensible defaults for our gradle projects to avoid boilerplate"
	labels = ["gradle", "gradle-plugin"]
	languageLevel = JavaVersion.VERSION_11
	configureProject(project)
}

apply from: "$rootDir/gradle/shipkit.gradle"

repositories {
	jcenter()
	maven { url = "https://plugins.gradle.org/m2/" }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 30, 'seconds' }

dependencies {
	api gradleApi()
	compileOnly "org.jetbrains:annotations:20.1.0"
	implementation "com.diffplug.spotless:spotless-plugin-gradle:5.9.0"
	implementation "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
	annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.3.0'
}

// per jabel docs
tasks.withType(JavaCompile) {
	options.compilerArgs.addAll([
		"--release", "11", // Avoid using Java 9+ APIs
		'--enable-preview',
	])
	// The following line can be omitted on Java 14 and higher
	options.compilerArgs << '-Xplugin:jabel'

	doFirst {
		options.compilerArgs = options.compilerArgs.findAll {
			it != '--enable-preview'
		}
	}
}

gradlePlugin {
	plugins {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			implementationClass = 'org.minimallycorrect.gradle.DefaultsPlugin'
		}
	}
}

pluginBundle {
	website = 'http://www.minimallycorrect.org/'
	vcsUrl = 'https://github.com/MinimallyCorrect/DefaultsPlugin'
	description = 'Sets up sensible defaults for our gradle projects to avoid boilerplate '
	tags = ['gradle', 'gradle-plugin']
	plugins << {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			displayName = 'MinimallyCorrect Defaults Plugin'
		}
	}
}
