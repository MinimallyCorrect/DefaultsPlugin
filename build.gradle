buildscript {
	repositories { jcenter() }
	dependencies { classpath 'org.minimallycorrect.gradle:DefaultsPlugin:0.0.29' }
}
plugins {
	id "java-gradle-plugin"
	id "maven"
	id "com.gradle.plugin-publish" version "0.9.2"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.diffplug.gradle.spotless" version "3.26.1"
}
apply plugin: 'org.minimallycorrect.gradle.DefaultsPlugin'

group="org.minimallycorrect.gradle"

minimallyCorrectDefaults {
	description = "Sets up sensible defaults for our gradle projects to avoid boilerplate"
	labels = ["gradle", "gradle-plugin"]
	lombokDependencyCoordinates.clear()
	lombokDependencyCoordinates.add("org.projectlombok:lombok:1.18.10")
	dependencyTargets.add("annotationProcessor")
	dependencyTargets.add("testAnnotationProcessor")
} ()

repositories {
	jcenter()
	maven { url = "https://repo.nallar.me/" }
	maven { url = "https://plugins.gradle.org/m2/" }
	maven { url = "https://files.minecraftforge.net/maven" }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 30, 'seconds' }

dependencies {
	compileOnly 'org.jetbrains:annotations:15.0'
	compileOnly "org.projectlombok:lombok:1.16.20"
	compileOnly("net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT") {
		transitive = false
	}
	compileOnly("gradle.plugin.com.matthewprenger:CurseGradle:1.0.9") {
		transitive = false
	}
	//compileOnly "gradle.plugin.com.github.spotbugs:gradlePlugin:1.3"
	compile "com.diffplug.spotless:spotless-plugin-gradle:3.26.1"
	compile "org.shipkit:shipkit:0.9.76"
}

gradlePlugin {
	plugins {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			implementationClass = 'org.minimallycorrect.gradle.DefaultsPlugin'
		}
	}
}

pluginBundle {
	website = 'http://www.minimallycorrect.org/'
	vcsUrl = 'https://github.com/MinimallyCorrect/DefaultsPlugin'
	description = 'Sets up sensible defaults for our gradle projects to avoid boilerplate '
	tags = ['gradle', 'gradle-plugin']
	plugins << {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			displayName = 'MinimallyCorrect Defaults Plugin'
		}
	}
}
