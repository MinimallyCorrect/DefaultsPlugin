buildscript {
	repositories { jcenter() }
	dependencies { classpath 'org.minimallycorrect.gradle:DefaultsPlugin:0.0.58' }
}
plugins {
	id "java-gradle-plugin"
	id "maven"
	id "com.gradle.plugin-publish" version "0.12.0"
}
apply plugin: 'org.minimallycorrect.gradle.DefaultsPlugin'

group="org.minimallycorrect.gradle"

minimallyCorrectDefaults {
	description = "Sets up sensible defaults for our gradle projects to avoid boilerplate"
	labels = ["gradle", "gradle-plugin"]
	languageLevel = JavaVersion.VERSION_11
	configureProject(project)
}

tasks.withType(JavaCompile) {
	options.compilerArgs.addAll(['--release', '8'])
}

repositories {
	jcenter()
	maven { url = "https://plugins.gradle.org/m2/" }
	maven { url = "https://files.minecraftforge.net/maven" }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 30, 'seconds' }

dependencies {
	api gradleApi()
	compileOnly 'com.google.guava:guava:30.0-jre'
	compileOnly("net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT") {
		transitive = false
	}
	compileOnly("gradle.plugin.com.matthewprenger:CurseGradle:1.4.0") {
		transitive = false
	}
	compileOnly "org.jetbrains:annotations:20.1.0"
	implementation "com.diffplug.spotless:spotless-plugin-gradle:5.8.2"
	implementation "org.shipkit:shipkit:2.2.7"
	annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.3.0'
}

// per jabel docs
tasks.withType(JavaCompile).all {
	options.compilerArgs = [
		"--release", "11", // Avoid using Java 9+ APIs
		'--enable-preview',
	]
	// The following line can be omitted on Java 14 and higher
	options.compilerArgs << '-Xplugin:jabel'

	doFirst {
		options.compilerArgs = options.compilerArgs.findAll {
			it != '--enable-preview'
		}
	}
}

gradlePlugin {
	plugins {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			implementationClass = 'org.minimallycorrect.gradle.DefaultsPlugin'
		}
	}
}

pluginBundle {
	website = 'http://www.minimallycorrect.org/'
	vcsUrl = 'https://github.com/MinimallyCorrect/DefaultsPlugin'
	description = 'Sets up sensible defaults for our gradle projects to avoid boilerplate '
	tags = ['gradle', 'gradle-plugin']
	plugins << {
		DefaultsPlugin {
			id = 'org.minimallycorrect.gradle.DefaultsPlugin'
			displayName = 'MinimallyCorrect Defaults Plugin'
		}
	}
}
